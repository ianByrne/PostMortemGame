# System topics
topic: ~xmain_control system ()

u: (\[ _*1 \] )
	# Parse OOB input
	$$inputOob = _0

	# Because OOB is passed every volley, it doesn't recognise a new conversation on its own, so need to hack it
	if(%more)
	{
		# Continue with the rest of the input
		^next(input)
	}
	else
	{
		# New conversation
		%length = 0
		%userfirstline = %input
		$_newInput = %input - 1
		%input = $_newInput
	}

u: (%bot==Olive %input<%userfirstline)
	^gambit(~olive_introductions)

u: (%bot==Clarence %input<%userfirstline ^CheckFact(^"CollectedFirstDelivery")==false)
	^gambit(~clarence_introductions)

u: (%bot==Cow %input<%userfirstline)
	^gambit(~cow_introductions)

u: (%bot==Reggie %input<%userfirstline)
	^gambit(~reggie_introductions)

u: ()
    $_responseCount = %response
    $$currenttopic = %topic

	if (%length == 0 AND %response == $_responseCount)
	{
		# gambit current topic since no input (usually start of conversation)
		^nofail(TOPIC ^gambit($$currenttopic))
	}

	if (%response == $_responseCount)
	{
		# try for rejoinders. might generate an answer directly from what we are looking for.
		^nofail(TOPIC ^rejoinder())
	}
	
	if (%response == $_responseCount)
	{
		# current topic tries to respond to his input
		^nofail(TOPIC ^respond($$currenttopic))
	}

	# see if some other topic has keywords matching his input (given we have no response yet)
	if (%response == $_responseCount)
	{
		@8 = ^keywordtopics()	# get topics referred in input
		^loop()
		{
			$$topic = first(@8subject)
			^nofail(TOPIC ^respond($$topic))
			if (%response != $_responseCount) # stop when we find something to say
			{
				^end(RULE)	# we are done, this terminates the loop (not the rule)
			}
		}
	}

	# if no topic reacts, go to the last ditch keywordless topic
	if (%response == $_responseCount) 
	{
		if(%bot == Olive)
		{
			^nofail(TOPIC ^respond(~olive_keywordless))
		}
		else if(%bot == Clarence)
		{
			^nofail(TOPIC ^respond(~clarence_keywordless))
		}
	}

	# gambit from current topic even though no keywords matched
	if (%response == $_responseCount)
	{
		^nofail(TOPIC ^gambit($$currenttopic))
	}

	# Completely out of ideas so fling some random gambits
	if (%response == $_responseCount)
	{
		@8 = ^GambitTopics()
		^loop()
		{
			$$topic = pick(@8subject)
			^nofail(TOPIC ^Gambit($$topic)) # try a topic at random
			if (%response != $_responseCount)
			{
				^end(RULE)
			}
		}
	}

	# Kind of out of ideas, so do a quibble
	if (%response == $_responseCount)
	{
		nofail(TOPIC respond(~QUIBBLE_ALL))
	}

	# if we have rejoinders for what we said OR we asked a question, stop here
	if (%outputrejoinder OR %lastquestion) 
	{
		^end(TOPIC)
	}
	
	# All out of ideas
	if (%response == $_responseCount)
	{
		^repeat()
		[Don't you have some deliveries to do...?]
		[So, anyway, I guess I should be getting back to what I was doing...]
		[Oh look! That other person is trying to get your attention]
	}

u: ()
	^repeat()
	An error occurred (missing input OOB)