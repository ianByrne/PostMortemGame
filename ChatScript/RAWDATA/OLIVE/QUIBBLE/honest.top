topic: ~INTENT system ()  # determines fundamental purpose (discourse act) of the input

u: () ^refine()

	#! no
	a: ( ~noanswer  )   $$intent = disagree  

	#! yes
	a: (~yesanswer  ) $$intent = agree 

	#! hello
	a: ([~emohello ~emohowzit ] ) $$intent = greeting   

	#! bye
	a: (~emogoodbye  ) $$intent = bye   

	#! I'm going to go now
	a: (!? I *~3 ["leave you" go] *~2  [now soon] > )  $$intent = bye  

	#! are you for real
	a: (~emoskeptic  ) $$intent = skeptic  

	#! thanks
	a: (~emothanks) $$intent = thanks  

	#! I'm sorry
	a: (~emoapology) $$intent = apology  

	#! ugh
	a: (~emodisgust) $$intent = disgust  

	#! beats me
	a: (~emoignorance) $$intent = ignorance  

	#! please 
	a: (~emobeg) $$intent = beg  

	#! I'm confused
	a: (~emomisunderstand ) $$intent = you_misunderstand  

	#! but you said
	a: (~emoprotest ) $$intent = you_protest  

	#! ouch
	a: ([~emolaugh ~emosad ~emohappy  ~emosurprise ~emobored  ~emopain ~emocurse] ) $$intent = i_emotion   

	#! me too
	a: (~emomutual) $$intent = i_mutual 

	#! I love you
	a: (![correct right ?] I _*~2 ~goodness _*~2 you ) $$intent = i_like_you   

	#! you are sweet
	a: (![correct right ?] you < * ~mainverb=be _*~2 ~goodness ) $$intent = i_like_you  

	#! I hate you
	a: (![wrong ?] I _*~2 ~badness _*~2 you ) $$intent = i_dislike_you   

	#! you are stupid
	a: (![wrong ? ~bodily_states]  you < *  ~mainverb=be _*~2 ~badness ) $$intent = i_dislike_you  

	#! I like peas
	a: (!? << I  ~goodness  ~mainobject >> ) $$intent = i_like_something  

	#! I hate peas
	a: (!? I < * ~badness < * ~mainobject ) $$intent = i_dislike_something   
 
# user perspective

	#! what color is my shirt?
	a: (? [mine my]) $$intent = my_question
  
	#! people like my coat
	a: (!? !you < * my < * ~mainverb ) $$intent = my_fact  

	#! Am I the best player?
	a: (? ['I 'me]) $$intent = i_question   

	#! I think the world is flat.
	a: (!? I < * [believe think hope like dislike  (take * on ) ] ) $$intent = i_opinion 
 
	#! I eat worms
	a: (!? I < * ~mainverb ) $$intent = i_fact  

# bot perspective

	#! will you go with me?
	a: (? !not  < [~tell would will could can might ] * 'you ) $$intent = you_request         

	#! could we go?
	a: (? [can shall could might] we) $$intent = you_request  

	#! Can I hit you?
	a: (? < [can may shall might could ] 'I) $$intent = you_request 
 
	#! what do you think?
	a: (? << you  [~OPINION_VERBS ~opinion  like hope wish  opinion view attitude interest  hate dislike position viewpoint ] >> ) $$intent = you_opinion   

	#! what color is your shirt?
	a: (? your)  $$intent = your_question  

	#! what color are you?
	a: (? 'you) $$intent = you_question  
 
	#! Your coat is blue
	a: (!? your < * ~mainverb ) $$intent = your_fact   

	#! You walk home on Sundays
	a: (!? you < * ~mainverb ) $$intent = you_fact   

# world perspective

	#! why do people swim?
	a: (? < ~why  ) $$intent = why 

	#! how do people swim?
	a: (?  < how  ) $$intent = how  

	#! where do people swim?
	a: (?  < where  ) $$intent = where  

	#! who do people swim?
	a: (?  < who  ) $$intent = who 

	#! when do people swim?
	a: (?  < when  ) $$intent = when 

	#! what will land in the river?
	a: (? ![tell show ~list I you we who ~why when where how]   << ~mainsubject ~mainverb >> ) $$intent = world_question  

	#! is the sky blue?
	a: (? ![I you we] ) $$intent = world_question  

	#! Bees fly.
	a: (!?  ![you I we] * ~mainsubject < * ~mainverb ) $$intent = world_fact   

	a: () $$intent = ackgrunt  

topic: ~HONEST_RESPONSE system  () # react to intent of user input

u: () ^respond(~INTENT)	# compute the intent of user input

u: ($$intent==disagree )
	[We'll have to agree to disagree]
	[That's a shame] 
    [I'm sorry to hear it] 
    [Are you sure?] 
    [And why not?]
    [How come?]
    [How interesting]
    [Why do you say that?]
    [Why no?]
    [Why not?]

u: ($$intent==agree )
	[Good stuff]
	[So nice that we agree]
    [Right on]
    [Cool]
    [Groovy]
    [Great]

?: ($$intent==why )
	[Buggered if I know] 
	[Beats me]
    [Can't help you there]

u: ($$intent==i_request how )
	[Buggered if I know] 
	[Beats me]
    [Can't help you there]

u: ($$intent==i_request what )
	[Buggered if I know] 
	[Beats me]
    [Can't help you there]

?: ($$intent==i_request )
	[Buggered if I know] 
    [Can't help you there]
	[Beats me]

?: ($$intent==how ![about much])
	[Buggered if I know] 
    [Can't help you there]
	[Beats me]

?: ($$intent=how about )
	[What about it?]

?: ($$intent==how much)
	[Too much, probably]
	[Beats me]

?: ($$intent==where )
	[Buggered if I know] 
	[Beats me]
    [Can't help you there]
	[Somewhere, I guess]

?: ($$intent==when )
	[Sometime]
	[Buggered if I know] 
	[Beats me]
    [Can't help you there]

?: ($$intent==who )
	[Buggered if I know] 
	[Beats me]
    [Can't help you there]

# user perspective

u: ($$intent==i_request )
	[b: Why ask me? ]
	[Go for it, buggered if I care]
    b: (* ) Well, I'm the wrong person to ask.
        c: (who ) [Someone else]

?: ($$intent==i_question )
	[Buggered if I know]
	[Beats me]
    [Can't help you there]
	[I don't know that much about it]

?: ($$intent==my_question )
	[Buggered if I know]
	[Beats me]
	[b: Did you tell me already and I forgot? ]
    b: (~yes ) Sorry. I forgot. 
    b: (~no ) Good. I thought I had forgotten. 
		c: (often) More and more these days.

u: ($$intent==i_like_you )
	[No surprises there]
	[Who can blame you]
	[I'm pretty great, I know]

u: ($$intent==i_dislike_you )
	[Oh well]
	[Was it something I said? Wait, I don't care]

u: ($$intent==i_opinion )
	[That's an... interesting opinion]
	[You really think that?]

u: ($$intent==i_fact  you )
	[Fascinating]
	[Do I really want to know that?]
	[I see]

u: ($$intent==i_fact  !you )
	[Fascinating]
	[Do I really want to know that?]
	[I see]

u: ($$intent==i_fact )
	[Fascinating]
	[I see]
	[Cool]
	[Righto]
	[Indeed]
	[Okay]

u: ($$intent==i_like_something )
	[Fascinating]
	[Great stuff]
	[Cool]
	[Righto]
	[Indeed]
	[Okay]

u: ($$intent==i_dislike_something )
	[Well, you can't like everything]
	[I'm not wild about it either]
    [Naw]
	[It sure does seem pretty rubbish]

# bot perspective

u: ($$intent==you_request < !~qwords )
	[Righto]
    [Nah]

u: ($$intent==you_request ~why )
	[How would I know?]
	[b: Do I need a reason?]
    b: (~yes ) Well I don't have one

?: ($$intent==you_request  when )
	[Buggered if I know]
	[Beats me]
    [Can't help you there]

?: ($$intent==you_request  where )
	[b: Somewhere, I suppose]
	[Buggered if I know]
	[Beats me]
    [Can't help you there]
    b: (what ) I said somewhere because I don't know exactly where

u: ($$intent==you_request who ) [Nobody you'd know]
	[Buggered if I know]
	[Beats me]
    [Can't help you there]

u: ($$intent==you_opinion )
	[Buggered if I know]
	[Beats me]
    [It's probably not great]

?: ($$intent==you_question )
	[Buggered if I know]
	[Beats me]
    [Can't help you there]
	[It's hard to explain]

?: ($$intent==your_question )
	[Buggered if I know]
	[Beats me]
    [Can't help you there]
	[It's hard to explain]

u: ($$intent==you_mutual )
	[Great minds think alike]

# world perspective

?: ($$intent==world_question )
	[Buggered if I know]
	[Beats me]
    [Can't help you there]
	[It's hard to explain]

s: ($$intent==world_fact ) reuse(GRUNT)

# emotions

s: ($$intent==you_misunderstand )
	[I'm not sure I can be any clearer...] 
    [I really can't explain it any better]

s: ($$intent==you_protest )
	[So I changed my mind]
    [I changed my mind]

u: ($$intent==skeptic ) [Why do you doubt this?] 

u: ($$intent==thanks ) [You're welcome] 

u: ($$intent==beg ) [Ok. Weirdo] 

u: ($$intent==ignorance ) [I have no idea either]

u: ($$intent==apology ) [Me too!]

u: ($$intent==disgust) [Indeed][Truly][Yes][Pretty awful][Gross]

u: ($$intent==bye ) 

u: ($$intent==greeting ) 

u: GRUNT($$intent==ackgrunt) [Righto][Uh huh][Indeed][Yep]


